#!/usr/bin/perl -w
# -*- perl -*-
# vim: ft=perl

=head1 NAME

SNMP__FreeNAS_loadaverage - Munin plugin to monitor FREENAS load averages via SNMP.
Based on the bundled snmp_load plug that shows 5min av.

=head1 APPLICABLE SYSTEMS

Tested on FreeNAS 9.10, prob works on 9.3 ;)

=head1 CONFIGURATION

As a rule SNMP plugins need site specific configuration.  The default
configuration (shown here) will only work on insecure sites/devices.

   [snmp_*]
	env.version 2
        env.community public

In general SNMP is not very secure at all unless you use SNMP version
3 which supports authentication and privacy (encryption).  But in any
case the community string for your devices should not be "public".

Please see 'perldoc Munin::Plugin::SNMP' for further configuration
information.

=head1 INTERPRETATION

Load average provides an indication of how many processes are
contending for the CPU.  Under normal conditions, it should be
less than or equal to the number of CPUs.  (Compare to the 'load'
Munin plugin.)

=head1 MIB INFORMATION

Not sure what MIB this uses, OID from: http://www.debianhelp.co.uk/linuxoids.htm


=head1 MAGIC MARKERS

  #%# family=snmpauto
  #%# capabilities=snmpconf

=head1 VERSION

  $Id$

=head1 BUGS

None known.

=head1 AUTHOR

Modified April 2016 by Nicholas Martin, University Of Warwick, UK.

Based on the snmp__load plugin by Jimmy Olsen, Dagfinn Ilmari Mannsaaker (2004)

=head1 LICENSE

GPLv2.

=cut

use strict;
use Munin::Plugin::SNMP;

if (defined $ARGV[0] and $ARGV[0] eq 'snmpconf') {
	# iso.org.dod.internet.private.enterprises.2021.10.1.3.1
	# (x-minute average)
	print "require .1.3.6.1.4.1.2021.10.1.3.1 [1-3]\n";
	exit 0;
}

if (defined $ARGV[0] and $ARGV[0] eq "config") {
	my ($host) = Munin::Plugin::SNMP->config_session();

	print "host_name $host\n" unless $host eq 'localhost';
	print <<"EOC";

graph_title Load average
graph_args --base 1000 -l 0 
graph_vlabel load
graph_category system
graph_info The load averages of the last 1, 5 & 15 minutes.
load1.label 1 min load av
load5.label 5 min load av
load15.label 15 min load av
load1.draw LINE
load1.colour COLOUR0
load5.draw LINE
load5.colour COLOUR2
load15.draw LINE
load15.colour COLOUR27
EOC
	exit 0;
}

my $session = Munin::Plugin::SNMP->session();
print "load1.value ", $session->get_single('.1.3.6.1.4.1.2021.10.1.3.1'), "\n";
print "load5.value ", $session->get_single('.1.3.6.1.4.1.2021.10.1.3.2'), "\n";
print "load15.value ", $session->get_single('.1.3.6.1.4.1.2021.10.1.3.3'), "\n";
